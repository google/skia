# Copyright 2018 Google Inc.
#
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  skia_enable_skottie = true
}

config("public_config") {
  if (skia_enable_skottie) {
    defines = [ "SK_ENABLE_SKOTTIE" ]
    include_dirs = [ "include" ]
  }
}

source_set("skottie") {
  if (skia_enable_skottie) {
    public_configs = [ ":public_config" ]
    sources = [
      "src/Skottie.cpp",
      "src/SkottieAdapter.cpp",
      "src/SkottieAnimator.cpp",
      "src/SkottieJson.cpp",
      "src/SkottieValue.cpp",
    ]
    configs += [ "../../:skia_private" ]
    deps = [
      "../..:skia",
      "../../third_party/rapidjson",
      "../sksg:sksg",
    ]
  }
}

source_set("fuzz") {
  if (skia_enable_skottie) {
    testonly = true

    public_configs = [ ":public_config" ]
    configs += [ "../..:skia_private" ]
    sources = [
      "fuzz/FuzzSkottieJSON.cpp",
    ]
    deps = [
      ":skottie",
      "../..:skia",  # TODO: refactor to make this nicer
    ]
  }
}
